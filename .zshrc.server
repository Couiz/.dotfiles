# === ZSH Config (Server) ===

# --- History ---
HISTFILE=~/.zsh_history
HISTSIZE=50000
SAVEHIST=50000
setopt SHARE_HISTORY HIST_IGNORE_DUPS HIST_IGNORE_SPACE HIST_REDUCE_BLANKS INC_APPEND_HISTORY

# --- Navigation ---
setopt AUTO_CD AUTO_PUSHD PUSHD_IGNORE_DUPS PUSHD_SILENT

# --- Completion ---
autoload -Uz compinit && compinit
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'
zstyle ':completion:*' menu select
zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}"
setopt COMPLETE_ALIASES

# --- Keybindings ---
# modifier+arrow: hardcoded xterm sequences (standard across all major frameworks)
bindkey '^[[1;5C' forward-word       # ctrl+right
bindkey '^[[1;5D' backward-word      # ctrl+left
bindkey '^[[1;3C' forward-word       # alt+right
bindkey '^[[1;3D' backward-word      # alt+left

# standard keys: use terminfo (sequences differ between terminal types)
[[ -n "${terminfo[khome]}" ]] && bindkey "${terminfo[khome]}" beginning-of-line  # home
[[ -n "${terminfo[kend]}"  ]] && bindkey "${terminfo[kend]}"  end-of-line        # end
[[ -n "${terminfo[kdch1]}" ]] && bindkey "${terminfo[kdch1]}" delete-char        # delete

# application mode: required for terminfo values to match actual sequences
# also handles tmux DA/OSC response leak workaround (tmux/tmux#4634)
zle-line-init() {
  (( ${+terminfo[smkx]} )) && echoti smkx
  [[ -n "$TMUX" ]] && while read -t 0.01 -s -k 1 2>/dev/null; do :; done
}
zle-line-finish() {
  (( ${+terminfo[rmkx]} )) && echoti rmkx
}
zle -N zle-line-init
zle -N zle-line-finish

# --- Prompt ---
command -v starship &>/dev/null && eval "$(starship init zsh)" || PROMPT='%n@%m:%~$ '

# --- Plugins (if available) ---
[ -s ~/.zsh/zsh-autosuggestions/zsh-autosuggestions.zsh ] && source ~/.zsh/zsh-autosuggestions/zsh-autosuggestions.zsh
[ -s ~/.zsh/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh ] && source ~/.zsh/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

# --- Tools ---
command -v zoxide &>/dev/null && eval "$(zoxide init zsh)"

# fzf
if [ -d /usr/share/doc/fzf/examples ]; then
  source /usr/share/doc/fzf/examples/key-bindings.zsh 2>/dev/null
  source /usr/share/doc/fzf/examples/completion.zsh 2>/dev/null
fi
if command -v fdfind &>/dev/null; then
  FD_CMD="fdfind"
elif command -v fd &>/dev/null; then
  FD_CMD="fd"
fi
if [ -n "$FD_CMD" ]; then
  export FZF_DEFAULT_COMMAND="$FD_CMD --type f --hidden --follow --exclude .git"
  export FZF_CTRL_T_COMMAND="$FD_CMD --type f --hidden --follow --exclude .git"
  export FZF_ALT_C_COMMAND="$FD_CMD --type d --hidden --follow --exclude .git"
fi
export FZF_DEFAULT_OPTS='--height 40% --layout=reverse --border'

# --- Aliases ---
alias ..='cd ..'
alias ...='cd ../..'
alias ....='cd ../../..'

# eza (modern ls replacement)
if command -v eza &>/dev/null; then
  alias ls='eza --icons --group-directories-first'
  alias ll='eza -l --icons --header --group-directories-first'
  alias la='eza -la --icons --header --group-directories-first'
  alias lt='eza --tree --level=2 --icons --group-directories-first'
  alias lg='eza -l --git --icons --header --group-directories-first'
fi

# --- Clipboard (OSC 52) ---
clip() {
  local data=$(cat "$@" | base64 | tr -d '\n')
  printf "\033]52;c;%s\a" "$data"
}

# --- Machine-specific config ---
[ -s "$HOME/.zshrc.local" ] && source "$HOME/.zshrc.local"
